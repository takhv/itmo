ORG	0x4F9 ; начало
RES:	WORD 0x62F ; результат
FINISH:	WORD 0x0A ; символ окончания
TEMP:	WORD ? ; запись нечет 
START:	CLA ; очистка аккум
S1:	IN 7 ; ввод первого символа из пары
	AND #0x40 ; проверка на 1 в статусном регистре
	BEQ S1 ; спин луп если 0 в ст. регистре
	IN 6 ; байт в аккум
	ST (RES) ; символ в результат
	ST TEMP ; держим в памяти
	CMP FINISH ; проверка на символ окончания
	BEQ EXIT ; выход если окончание
	CLA ; очистка аккум
S2:	IN 7 ; ввод второго символа из пары
	AND #0x40 ; проверка на 1 в статусном регистре
	BEQ S2 ; спин луп если 0 в ст. регистре
	IN 6 ; байт в аккум
	SWAB ; скидываем второй символ в старший байт
	OR TEMP ; соединяем с первым символом
	ST (RES) ; сохраняем в результат
	SUB TEMP ; убираем первый символ
	SWAB ; возвращаем второй символ в младший байт
	CMP FINISH ; проверка на символ окончания
	BEQ EXIT ; выход если окончание
	LD (RES)+ ; ссылка на результат+1
	CLA ; очистка аккум
	JUMP S1 ; прыгаем в начало
EXIT:	LD (RES)+ ; ссылка на результат+1
	HLT ; остановка
